{//;    Advanced bank system                
        Created by donutMind                }

{//;    Dependencies:                       
        randhex.API - byParallax            }

{//;    Note
        If you want to restrict withdraw
        Uncomment the {if} line below
        right after 'withdraw;'

        Otherwise, leave it as is    
                                            }

{//;Get subcommand name and do specified
    action based on subcommand name         }
{switch;{lower;{args;0}};
    deposit;
        {set;~deposit;{abs;{parseint;{args;1}}}}
        {if;{get;~deposit};==;NaN;That's not a number!{return}}
        {set;_{userid}_store;{math;+;0{get;~deposit};0{get;_{userid}_store}}}
        {embed;{embedbuild;
            title:Deposit;
            description:You deposit {get;~deposit} dollars.{newline}Your current total: {get;_{userid}_store} dollars.;
            color:{exec;randhex;api};
        }}
    ;
    withdraw;
        {//;if;{userhasrole;ROLE_ID;{userid}};==;true;;You can't withdraw money!{return}}
        {set;~withdraw;{abs;{parseint;{args;1}}}}
        {if;{get;~withdraw};>;10000;You can't withdraw more than 10000 at once!{return}}
        {if;{get;_{userid}_store};<;{get;~withdraw};Your total is less than you can withdraw!{return}}
        {set;_{userid}_store;{math;-;0{get;_{userid}_store};0{get;~withdraw}}}
        {embed;{embedbuild;
            title:Withdraw;
            description: You withdrew {get;~withdraw} dollars.{newline}Your current total: {get;_{userid}_store} dollars.;
            color:{exec;randhex;api};
        }}
    ;
    balance;
        {embed;{embedbuild;
            title:Account balance;
            description:Your current total is: {get;_{userid}_store} dollars.;
            color:{exec;randhex;api};
        }}
    ;

    help;
        {embed;{embedbuild;
            title:Account help;
            description:Help for the `account` command;
            fields.name:Deposit;
            fields.value:Deposit money.;
            fields.inline:true;
            fields.name:Withdraw;
            fields.value:Withdraw some money. Max amount per usage - 10000;
            fields.inline:true;
            fields.name:Balance;
            fields.value:Check balance;
            color:{exec;randhex;api};
            footer.text:Sent by {username};
            timestamp:{time};
        }}
    ;
    {embed;{embedbuild;
        title:Exception;
        description:Please do `b!t account help` for help;
        color:Red;
    }}
}
