{//;Check ToU acceptance}
{if;{get;@pup.rules{userid}};!=;true;{exec;tou}{waitmessage;;{userid};{if;{bool;{messagetext};startswith;{lower;yes}};==;true;{set;@pup.rules{userid};true}}}{return}}

{//;Set dynamic command usage}
{if;{iscc};==;true;{set;~cmd;{prefix}{commandname}};{set;~cmd;{prefix}t {commandname}}}

{//;View puppy function}
{function;view;
    {//;To view other puppies...}
    {if;{argslength};>;2;{set;~user;{userid;{args;1;n}}};{set;~user;{userid}}}
    {if;{get;@pup.own{get;~user}};!=;true;This user doesn't has puppy!{return}}
    {embed;
        {embedbuild;
            title:{get;@pup.name{get;~user}} stats;
            fields.name:Name;
            fields.value:{get;@pup.name{get;~user}};
            fields.inline:true;
            fields.name:Adopt date;
            fields.value:{get;@pup.date{get;~user}};
            fields.inline:true;
            fields.name:Hungry;
            fields.value:{get;@pup.hungry{get;~user}};
            fields.inline:true;
            fields.name:Clean;
            fields.value:{get;@pup.clean{get;~user}};
            image.url:{get;@pup.image{get;~user}};
            color:White;
        }
    }
}

{//;Dynamic help}
{function;help;
    {embed;
        {embedbuild;
            title:Puppy handbook 101;
            description:__General-and-Customize__:{newline}{get;~cmd} Adopt - adopt puppy.{newline}{get;~cmd} Release - release puppy.{newline}{get;~cmd} view <user> - view puppies of other users (if they have one).{newline}{get;~cmd} Name - give your puppy a new name. {newline}{get;~cmd} Avatar - give your pup a new look (alias - look).{newline;2}__Care__:{newline}{get;~cmd} Rest - let your puppy sleep for a while.{newline}{get;~cmd} Feed - feed puppy (once per 24h).{newline}{get;~cmd} Clean - wash your puppy.{newline;2}__Fun__:{newline}{get;~cmd} Play - play with your pup.{newline}**Note** that he will get tired if you do this very often.{newline;2} Pat - slightly pat your puppy.;
            color:white;
            footer.text:Live - 25.08.2018;
            timestamp:{time};
        }
    }
}

{//;Validate image (i.e. image is not blank/empty)}
{function;valid_image;
    {if;{logic;!;{regextest;{args;1};/^[(http(s)?):\/\/(www\.)?a-zA-Z0-9@:%._\+~#=]{2,256}\.[a-z]{2,6}\b([-a-zA-Z0-9@:%_\+.~#?&//=]*)$/}};false{return}}
    {set;~r;{logic;!;{regextest;{messageembeds;{get;~msgid}};/^\[\{"fields":\[\{"inline":false,"name":"Malformed JSON","value":"`Invalid embed: Invalid image\.url`"\}\],"type":"rich"\}\]$|"image":\{"url":".+?","width":(?:0,"proxy_url":".+?","height":0|1200,"proxy_url":".+?","height":630)\}/}}}
}

{switch;{lower;{args;0}}
    ;help;
        {func.help}
    ;adopt;
        {if;{get;@pup.own{userid}};==;true;You can't have more than one puppy!{return}}
        {embed;
            {embedbuild;
                title:You adopted a nice and funny pup!;
                description:Don't forget to take care of it!;
                color:White;
                image.url:{get;@pup.image{userid}};
            }
        }
        {set;@pup.own{userid};true}
        {set;@pup.name{userid};Puppy}
        {set;@pup.image{userid};https://www.pixelstalk.net/wp-content/uploads/2016/08/Cute-Puppy-HD-Wallpaper.jpg}
        {set;@pup.date{userid};{time;YYYY-MM-DD HH:mm:ss}}
        {set;@pup.balance{userid};0} 
    ;release;
        {if;{get;@pup.own{userid}};!=;true;What you are trying to release? Air?{return}}
        Congrats, you released your puppy! (Actually, no, you are an awful person)
        {set;@pup.own{userid};false}
        {set;@pup.name{userid};Pup}
        {set;@pup.image{userid};https://www.pixelstalk.net/wp-content/uploads/2016/08/Cute-Puppy-HD-Wallpaper.jpg}
        {set;@pup.date{userid};You haven't pup!}
        {set;@pup.balance{userid};0}
    ;feed;
        {if;{get;@pup.own{userid}};!=;true;What you are trying to feed with dog food? Yourself?{return}}
        Your puppy grows as you feed it!
        {set;@pup.balance{userid};{math;+;0{get;@pup.balance{userid}};{randint;1;15}}}
    ;rest;
        {if;{get;@pup.own{userid}};!=;true;Your puppy can't rest, if you don't have one!<:blobhyperthink:477198810456653834>{newline}Do `{get;~cmd} adopt` to start!{return}}
        {if;{get;@pup{userid}d_rest};==;{time;DD};;Your puppy wants to play some more!{return}}
        {if;{get;@pup{userid}h_rest};==;{time;HH};
            {set;@pup{userid}sleep;false}{set;@pup{userid}d_rest;{math;+;{time;DD};1}}{set;@pup{userid}h_rest;{time;HH}}Your puppy is laying down to sleep for a while. Come back later!{return};Your puppy doesn't want to sleep now!{return}}
        {set;@pup.balance{userid};{math;+;0{get;@pup.balance{userid}}+{randint;1;15}}}
    ;play;
        {if;{get;@pup.own{userid}};!=;true;As you don't have puppy, you start playing with yourself.Do `{get;~cmd} adopt` to start!{return}}
        You play with your puppy and you and your little pet become happier!
        {set;@pup.balance{userid};{math;+;0{get;@pup.balance{userid}}+{randint;1;15}}}
    ;pat;
        {if;{get;@pup.own{userid}};!=;true;You don't have puppy. Do `{get;~cmd} adopt` to start!{return}}
        You slightly pat your little doggo, and he likes it!
        {set;@pup.balance{userid};{math;+;0{get;@pup.balance{userid}}+{randint;1;15}}}
    ;clean;
        {if;{get;@pup{userid}clean};==;true;Your pup doesn't want to wash!{return};Your pup is running to bathroom very fast, and 20 mitues later he becomes clean!{set;@pup{userid}clean;true}}
        {set;@pup.balance{userid};{math;+;0{get;@pup.balance{userid}};{randint;1;15}}}
    ;name;
        {if;{get;@pup.own{userid}};!=;true;You don't have a pup to give a name!{return}}
        {if;{argslength};>;1;{set;@pup.name{userid};{args;1;n}}ðŸ‘Œ;Oops, you didn't type a name? {return}}
    ;["avatar","look"];
        {if;{argslength};>=;2;;Oops, image not found. Maybe, you didn't provide it?{return}}
        {if;{get;@pup.own{userid}};==;false;You don't have a pup to set new look!{return}}
        {if;{get;@pup.own{userid}};==;;You don't have a pup to set new look!{return}}
        {if;{regextest;{args;1};/\bhttps?:[^)''"]+\.(?:jpg|jpeg|gif|png)(?![a-z/])/g};==;false;Not an image! Possible image formats: jpg | jpeg | png | gif{return}}
        {if;{func.valid_image};==;true;;Provided image is invalid or empty!{return}}
    ;shop;
        {embed;{embedbuild;
            title:Shop;
            fields.name:Food;
            fields.value:Canned meat (5 coins){newline}Meat with bones (10 coins){newline}Fresh meat (15 coins);
            fields.name:Toys & Play;
            fields.value:Tennis ball (2 coins){newline}Frisbee (5 coins){newline}Raccoon toy (20 coins);
            fields.name:Miscellaneous items;
            fields.value:Raincoat (45 coins);
            color:white;
            footer.text:Your balance: {get;@pup.balance{userid}} coins | Wanna buy something? Do `{get;~cmd} buy <item>`!;
        }}
    ;buy;
        {func.purchase;{args;1;n}};
    {//;If no subcommand, display puppy stats}
    {func.view}
}